REQUIRE "atoms.a4l";
REQUIRE "vikram/mytypes.a4l";

REQUIRE "johnpye/thermo_types.a4c";


MODEL Qabsorbed;

	value 			IS_A intensity;
	DNI 			IS_A intensity;
	THETA 			IS_A angle;
	THETAz	 		IS_A angle;
	DEL 			IS_A angle;
	OMEGA			IS_A angle;
	n 				IS_A time;
	Solar_Time		IS_A time;
	Standard_Time	IS_A time;
	DST				IS_A time;
	Lst				IS_A angle;
	Lloc			IS_A angle;
	E				IS_A time;
	B				IS_A angle;
	PHI				IS_A angle;
	Iam				IS_A factor;
	Rs				IS_A factor;
	Weff			IS_A distance;
	W				IS_A distance;
	Lspacing		IS_A distance;
	ENDLOSS			IS_A factor;
	F				IS_A distance;

	Lsca			IS_A distance;
	Nfield			IS_A fraction;
	Nhce			IS_A fraction;
	SFAavail		IS_A fraction;


	value = DNI * cos(THETA) * Iam * Rs * ENDLOSS * Nfield * Nhce * SFAavail;

	THETA = arccos( (cos(THETAz)^2 + (cos(DEL)^2)*(sin(OMEGA)^2))^0.5);
	
	DEL = 23.45 * sin(360*( 284+n )/365);
	OMEGA = (Solar_Time - 12) * 15 *(3.14/180);
	Solar_Time = Standard_Time - DST + (Lst - Lloc)/15{deg/hour} + E*(1{hour})/(60{min});
	E = 229.18{min} * (0.000075 + 0.001868*cos(B) - 0.032077*sin(B) - 0.014615*cos(2*B) - 0.04089*sin(2*B));
	B = (360/365) * (n-1) * (3.14/180);
	THETAz = cos(DEL)*cos(PHI)*cos(OMEGA) + sin(DEL)*sin(PHI);
	
	Iam = 1 + 8.84e-4 * (THETA/cos(THETA)) - 5.369e-5 * (THETA^2)/cos(THETA);
	
	Rs = Weff / W;
	Rs = (Lspacing/W) * (cos(THETAz)/cos(THETA));
	
	ENDLOSS = 1 - F * tan(THETA) / Lsca;
	
	
	UNITS
	heat_intensity = {watt/m^2};
	END UNITS;
	

	METHODS
		METHOD specify;
			FIX DNI;
			FIX n;
			FIX Lst;
			FIX Lloc;
			FIX DST;
			FIX Standard_Time;
			FIX Lspacing;
			FIX W;
			FIX F;

			FIX Lsca;
			FIX Nfield;
			FIX Nhce;
			FIX SFAavail;
			FIX PHI;
		END specify;	
		
		METHOD values;
			DNI := 60 {watt/m^2};
			n := 31 {day}; (* 31st January *)
			Lst := -105 {deg};
			Lloc := -110 {deg};
			DST := 0 {hour};
			PHI := 37.21 {deg};
			Standard_Time := 12 {hour};
			Lspacing := 15 {m};
			W := 5 {m};
			F := 5 {m};

			Lsca := 50 {m};
			Nfield := 0.857;
			Nhce := 0.832;
			SFAavail := 1;
			
			(* guesses *)
			value := 100 {watt/m^2};
		END values;

		METHOD on_load;
			RUN specify;
			RUN values;
		END on_load;

END Qabsorbed;


MODEL test_Qabsorbed;

	Qabs IS_A Qabsorbed;

	METHODS	
		METHOD on_load;
			RUN Qabs.specify;
			RUN Qabs.values;
		END on_load;
		
END test_Qabsorbed;


MODEL HeatLossField;
	DNI IS_A intensity;
	Ti IS_A temperature;
	To IS_A temperature;
	
	type IS_A variable;
	HCEfra IS_A fraction;

	a0 IS_A solver_var;
	a1 IS_A solver_var;
	a2 IS_A solver_var;
	a3 IS_A solver_var;
	b0 IS_A solver_var;
	b1 IS_A solver_var;
	
	HLfield IS_A heatloss_per_unit_lenght;

	HLfield * (To-Ti) = HCEfra * (a0*(To-Ti) + (a1/2)*(To^2 - Ti^2) + (a2/3)*(To^3 - Ti^3) + (a3/4)*(To^4 - Ti^4) * DNI*(b0*(To-Ti) + (b1/3)*(To^3 - Ti^3)));

	METHODS	
		METHOD specify;		
			FIX a0;
			FIX a1;
			FIX a2;
			FIX a3;
			FIX b0;
			FIX b1;
			
			FIX DNI;
			FIX Ti;			
		END specify;
	
		METHOD values;		
			DNI := 60 {watt/m^2};
			Ti := 30 {K};
		
			IF (type = 1) THEN
			(* air *)
			a0 := -2.247372E+01 {watt/m^2};
			a1 :=  8.374490E-01 {watt/m^2/K};
			a2 :=  0.00         {watt/m^2/K^2};
			a3 :=  4.620143E-06 {watt/m^2/K^3};
			b0 :=  6.983190E-02	{m};
			b1 :=  9.312703E-08 {m/K^2};
			END IF;
			
			IF (type = 2) THEN
			(* vaccum *)
			a0 := -2.247372E+01 {watt/m^2};
			a1 :=  8.374490E-01 {watt/m^2/K};
			a2 :=  0.00         {watt/m^2/K^2};
			a3 :=  4.620143E-06 {watt/m^2/K^3};
			b0 :=  6.983190E-02	{m};
			b1 :=  9.312703E-08 {m/K^2};
			END IF;
			
			IF (type = 3) THEN
			(* hydrogen *)
			a0 := -2.247372E+01 {watt/m^2};
			a1 :=  8.374490E-01 {watt/m^2/K};
			a2 :=  0.00         {watt/m^2/K^2};
			a3 :=  4.620143E-06 {watt/m^2/K^3};
			b0 :=  6.983190E-02	{m};
			b1 :=  9.312703E-08 {m/K^2};
			END IF;
		END values;		
END HeatLossField;




MODEL Qcollected;
	
	(* variables *)
    field_types IS_A set OF symbol_constant;
    field_types :== ['air','vaccum','hydrogen'];


	DNI IS_A intensity;
	
	Ti IS_A temperature;
	To IS_A temperature;
	Tambient IS_A temperature;
	
	width IS_A distance;	
	
	RecHL IS_A intensity;
	SfHL IS_A intensity;
	
	Qcoll IS_A intensity;
	Qabs IS_A Qabsorbed;
	delT IS_A delta_temperature;

	rho_i IS_A mass_density;
	hi IS_A specific_enthalpy;
	
	ho IS_A specific_enthalpy;
		
	delHfield IS_A specific_enthalpy;
	
	Lsca IS_A distance;
	Nsca IS_A factor;
	Vhtf IS_A volumetric_flowrate;


	(* parts *)
	hlfield[field_types] IS_A HeatLossField;
	
	
	(* interconnecting *)
    To,
    hlfield['air'].To,
    hlfield['vaccum'].To,
    hlfield['hydrogen'].To          ARE_THE_SAME;

	
	(* equations *)

	RecHL * width = SUM[hlfield[field_types].HLfield];
	
	delT * 2 = (To + Ti) - Tambient * 2;
	SfHL = 0.01693{watt/m^2/K}*delT - 0.0001683{watt/m^2/K^2}*(delT^2) + 0.78e-7{watt/m^2/K^3}*(delT^3);
	
	Qcoll = Qabs.value - (RecHL + SfHL);


	rho_i = 1074.0{kg/m^3} - 0.6367{kg/m^3/K}*Ti - 0.0007762{kg/m^3/K^2}*Ti^2;
	delHfield * (Vhtf * rho_i) = Qcoll * width * Lsca * Nsca;

	hi = 1000{m^2/s^2} * (-18.34 + 1.498{1/K}*Ti + 0.001377{1/K^2}*Ti^2);
	ho = 1000{m^2/s^2} * (-18.34 + 1.498{1/K}*To + 0.001377{1/K^2}*To^2);
	delHfield = abs(hi - ho);
	
	
	METHODS	
		METHOD specify;		
			FIX DNI;
			FIX Ti;
			FIX Tambient;
			
			FIX width;
			FIX Lsca;
			FIX Nsca;
			FIX Vhtf;

			FIX hlfield[field_types].HCEfra;
			FIX hlfield[field_types].type;
			
			RUN hlfield[field_types].specify;
			
			RUN Qabs.specify;
		END specify;
		
		METHOD values;
			DNI := 60 {watt/m^2};
			Ti := 60{K};
			Tambient := 30{K};
			
			width := 5{m};
			Lsca := 50 {m};
			Nsca := 256;
			Vhtf := 400{m^3/hour};

			hlfield['air'].type := 1;
			hlfield['vaccum'].type := 2;
			hlfield['hydrogen'].type := 3;

			hlfield['air'].HCEfra := 1.0;
			hlfield['vaccum'].HCEfra := 0.0;
			hlfield['hydrogen'].HCEfra := 0.0;

			RUN hlfield[field_types].values;
			RUN Qabs.values;

			(* guesses *)
			To := 100{K};
		END values;
		
		METHOD on_load;
			RUN specify;
			RUN values;
		END on_load;
END Qcollected;



MODEL test_Qcollected;

	Qcoll IS_A Qcollected;

	METHODS
		METHOD on_load;
			RUN Qcoll.specify;
			RUN Qcoll.values;
		END on_load;
		
END test_Qcollected;

