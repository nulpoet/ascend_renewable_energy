REQUIRE "atoms.a4l";


MODEL heat_transfer_coefficient_radiance(
	T1 WILL_BE temperature;
	T2 WILL_BE temperature;
	E1 WILL_BE solver_var;
	E2 WILL_BE solver_var;
	);

	value IS_A solver_var;
	
	value = 5.67e-8*(T1 + T2)*(T1^2 + T2^2) / (1/E1 + 1/E2 - 1);	
	
END heat_transfer_coefficient_radiance;


MODEL test_radiance;

	T1 IS_A temperature;
	T2 IS_A temperature;
	E1 IS_A solver_var;
	E2 IS_A solver_var;
	
	h_r IS_A heat_transfer_coefficient_radiance(T1, T2, E1, E2);

	METHODS
		METHOD specify;
			FIX T1;
			FIX T2;
			FIX E1;
			FIX E2;
		END specify;
		METHOD values;
			T1 := 60 {K} + 273.15 {K};
			T2 := 30 {K} + 273.15 {K};
			E1 := 0.6;
			E2 := 0.7;
		END values;
		METHOD on_load;
			RUN specify;
			RUN values;
		END on_load;	

END test_radiance;


MODEL Nusselt_number(
	T1 WILL_BE temperature;
	T2 WILL_BE temperature;
	B WILL_BE angle;
	L WILL_BE distance;
	);

	g IS_A acceleration;
	value IS_A solver_var;
	dT IS_A solver_var;
	Ra IS_A solver_var;
	t1 IS_A solver_var;
	t2 IS_A solver_var;
	tt1 IS_A solver_var;
	tt2 IS_A solver_var;
	
	g = 9.8 {m/s^2};
	dT = abs(T1-T2);
	Ra = g * B * dT * L^3;

	tt1 = (1 - 1708/(Ra*cos(B) ) );
	t1 = (abs(tt1) + tt1 )/ 2;
	tt2 = (Ra * cos(B)/5830)^(0.3) - 1;
	t2 = (abs(tt2) + tt2 )/ 2;
	
	value = 1 + 1.44 * (1- 1708*((sin(1.8 * B)) ^ 1.6) / (Ra * cos(B))) * t1 + t2;
END Nusselt_number;


MODEL test_Nusselt_number;
	T1 IS_A temperature;
	T2 IS_A temperature;
	B IS_A angle;
	L IS_A distance;

	h_c IS_A Nusselt_number(T1, T2, B, L);

	METHODS
		METHOD specify;
			FIX T1;
			FIX T2;
			FIX B;
			FIX L;
		END specify;
		METHOD values;
			T1 := 60 {K} + 273.15 {K};
			T2 := 30 {K} + 273.15 {K};
			B := 0.85 {rad};
			L := 1.8 {cm};
		END values;
		METHOD on_load;
			RUN specify;
			RUN values;
		END on_load;	
END test_Nusselt_number;


MODEL air_themal_conductivity;
	value IS_A solver_var;
	value = 0.0253;
END air_themal_conductivity;


MODEL heat_transfer_coefficient_convection(
	T1 WILL_BE temperature;
	T2 WILL_BE temperature;
	spacing  WILL_BE distance;
	B WILL_BE angle;
	);
	
	value IS_A solver_var;
	
	Nu IS_A Nusselt_number(T1, T2, B, spacing);
	K IS_A air_themal_conductivity;
		
	value = Nu.value * K.value / spacing;	
END heat_transfer_coefficient_convection;


MODEL cover(
	T WILL_BE temperature;
	T_ WILL_BE temperature;
	E WILL_BE fraction;
	E_ WILL_BE fraction;
	spacing WILL_BE distance;
	tilt WILL_BE angle;
);
	
	thermal_resistance IS_A solver_var;
	
	h_r IS_A heat_transfer_coefficient_radiance(T, T_, E, E_ );
    h_c IS_A heat_transfer_coefficient_convection(T, T_, spacing, tilt );
    
	thermal_resistance = 1/(h_c.value + h_r.value );

	METHODS
		METHOD specify;
			FIX E;
			FIX spacing;
			FIX tilt;

		END specify;		
END cover;

MODEL test_cover;
	T IS_A temperature;
	T_ IS_A temperature;
	E IS_A fraction;
	E_ IS_A fraction;
	spacing IS_A distance;
	tilt IS_A angle;

    Cover IS_A cover(T, T_, E, E_, spacing,	tilt);

	METHODS
		METHOD values;		
		    T := 60 {K} + 273.15 {K};
		    T_ := 90 {K} + 273.15 {K};

   		    E := 0.8;
   		    E_ := 0.95;
		    
		    spacing := 2 {cm};		    
			tilt := 0.785 {rad};
		END values;
		
		METHOD specify;			
		    FIX T;
		    FIX T_;
		    FIX E;
		    FIX E_;
		    FIX spacing;
		    FIX tilt;
		END specify;
	
		METHOD on_load;
			RUN specify;
			RUN values;
		END on_load;
END test_cover;

MODEL topcover_to_atmosphere(
	Ta WILL_BE temperature;
	Ttop WILL_BE temperature;
	Etop WILL_BE fraction;
);
	Ts IS_A solver_var;
	thermal_resistance IS_A solver_var;
	h_r IS_A solver_var;
	h_c IS_A solver_var;

	Ts = 0.0552*(Ta^1.5);
	h_r * (Ttop - Ta) = Etop * 5.67e-8 * (Ttop^4 - Ts^4);
	
	h_c = 10;
	
	thermal_resistance = 1/(h_r + h_c );
	
	METHODS
		METHOD specify;
			FIX h_c_a;
			FIX Ta;
			FIX Etop;
		END specify;
END topcover_to_atmosphere;


MODEL test_topcover_to_atmosphere;
	Ta IS_A temperature;
	Ttop IS_A temperature;
	Etop IS_A fraction;

    Rtop IS_A topcover_to_atmosphere (Ta, Ttop, Etop);

	METHODS
		METHOD values;		
		    Ta := 30 {K} + 273.15 {K};
		    Ttop := 60 {K} + 273.15 {K};

   		    Etop := 0.8;
		END values;
		
		METHOD specify;			
		    FIX Ta;
		    FIX Ttop;
		    FIX Etop;
		END specify;
	
		METHOD on_load;
			RUN specify;
			RUN values;
		END on_load;
END test_topcover_to_atmosphere;


MODEL absorbed_radiations;
	value IS_A solver_var;	
	METHODS
		METHOD specify;
			FIX value;
		END specify;
END absorbed_radiations;


MODEL heat_loss(
	Tp WILL_BE temperature;
	Ta WILL_BE temperature;
	Cover WILL_BE cover;
	Rtop WILL_BE topcover_to_atmosphere;
);
	value IS_A solver_var;
	Ut IS_A solver_var;
	
	Ut * ( Cover.thermal_resistance + Rtop.thermal_resistance ) = 1;
	value = Ut*(Tp-Ta);
	
	METHODS
		METHOD specify;
			FIX value;
		END specify;
END heat_loss;


MODEL test_heat_loss;
	Ta IS_A temperature;
	Ttop IS_A temperature;
	Etop IS_A fraction;

    Rtop IS_A topcover_to_atmosphere (Ta, Ttop, Etop);

	T IS_A temperature;
	T_ IS_A temperature;
	E IS_A fraction;
	E_ IS_A fraction;
	spacing IS_A distance;
	tilt IS_A angle;

    Cover IS_A cover(T, T_, E, E_, spacing,	tilt);

	Tp IS_A temperature;

	Heat_Loss IS_A heat_loss (Tp, Ta, Cover, Rtop);

	METHODS
		METHOD values;		
		    Ta := 30 {K} + 273.15 {K};
		    Ttop := 60 {K} + 273.15 {K};
   		    Etop := 0.8;
   		    
   		    T := 60 {K} + 273.15 {K};
   		    T_ := 90 {K} + 273.15 {K};
   		    E := 0.8;
   		    E_ := 0.95;		    
		    spacing := 2 {cm};		    
			tilt := 0.785 {rad};
		
   		    Tp := 90 {K} + 273.15 {K};
		END values;
		
		METHOD specify;			
		    FIX Ta;
		    FIX Ttop;
		    FIX Etop;
		    FIX T;
		    FIX T_;
		    FIX E;
		    FIX E_;
		    FIX spacing;
		    FIX tilt;
		    FIX Tp;
		END specify;
	
		METHOD on_load;
			RUN specify;
			RUN values;
		END on_load;
END test_heat_loss;


MODEL flat_plate_collector;
	A IS_A area;
	tilt IS_A angle;
	Heat_Absorbed IS_A absorbed_radiations;	
	Heat_Loss IS_A heat_loss (Tp, Ta, Cover, Rtop);
	Ta IS_A temperature;
	Tp IS_A temperature;
	Ep IS_A fraction;
	Ec IS_A fraction;
	cover_spacing IS_A distance;

	Tc IS_A temperature;

	Qu IS_A solver_var;	
	
	Cover IS_A cover (Tc, Tp, Ec, Ep, cover_spacing, tilt);
	Rtop IS_A topcover_to_atmosphere (Ta, Tc, Ec);

	Cover.T = Tp - Cover.thermal_resistance * Heat_Loss.Ut * (Tp-Ta);
	
	Qu = A * (Heat_Absorbed.value - Heat_Loss.value);
	
	METHODS
		METHOD specify;			
			FIX A;
			FIX tilt;
			FIX Heat_Absorbed.value;
			FIX Ta;
			FIX Tp;
			FIX Ep;

	    	FIX Ec;
	    	FIX cover_spacing;
	        RUN Cover.specify;

		END specify;		
END flat_plate_collector;


MODEL test_flat_plate_collector;
    fpcollector IS_A flat_plate_collector;

	METHODS
		METHOD values;
		
			fpcollector.Heat_Absorbed.value := 10000000 {J/m^2};
		
		    fpcollector.Ta := 30 {K} + 273.15 {K};
   		    fpcollector.Tp := 90 {K} + 273.15 {K};

		    
		    fpcollector.Ep := 0.95;
		    
		    fpcollector.A := 1 {m^2};
		
			fpcollector.tilt := 0.785 {rad};

			fpcollector.Ec 		:= 	0.8;
			fpcollector.cover_spacing 	:= 	2 {cm};
		END values;
		
		METHOD specify;			
		    RUN fpcollector.specify;
		END specify;
	
		METHOD on_load;
			RUN specify;
			RUN values;
		END on_load;		
END test_flat_plate_collector;

